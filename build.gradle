buildscript {
    repositories { mavenCentral() }
}

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'io.papermc.paperweight.userdev' version '1.7.1'
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'io.papermc.paperweight.userdev'

    repositories {
        mavenCentral()
        maven { url = 'https://libraries.minecraft.net' }
        maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
        maven { url 'https://repo.codemc.io/repository/maven-releases/' }
    }

    dependencies {
        testImplementation platform('org.junit:junit-bom:5.9.1')
        testImplementation 'org.junit.jupiter:junit-jupiter'
        paperweight.paperDevBundle(paperDevBundleVersion)
        compileOnly 'com.mojang:brigadier:1.0.18'
        compileOnly 'me.clip:placeholderapi:2.11.5'
        compileOnly 'com.arcaniax:HeadDatabase-API:1.3.1'
        compileOnly 'com.github.retrooper.packetevents:spigot:2.3.0'
    }

    test {
        useJUnitPlatform()
    }
}

group = 'dev.fabled'

version = "${majorVersion}.${minorVersion}.${patchVersion}"
if (snapshot) {
    version += "-SNAPSHOT-${buildNumber}"
}

subprojects {
    group = rootProject.group
    version = rootProject.version
}

dependencies {
    implementation project(path: ':armor')
    implementation project(':common')
    implementation project(':mines')
    implementation project(':omnitool')
    implementation project(':privatemines')
    implementation project(':explosives')
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

// Not needed on Paper 1.20.5+
//assemble.dependsOn(reobfJar)
assemble.dependsOn(shadowJar)

shadowJar {
    setArchiveClassifier(null)
    setArchiveFileName(getArchiveFileName().get().replace("-dev-all", ""))
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'

    filesMatching('paper-plugin.yml') {
        expand props
    }

    // Ensure the config.yml is included in the JAR
    from(sourceSets.main.resources.srcDirs) {
        include 'explosives/rpgconfig.yml'
    }

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}