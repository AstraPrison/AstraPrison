plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'astra.modules-common'
    id 'io.papermc.paperweight.userdev' version '1.7.1'
}

group = 'dev.fabled'
version = "${majorVersion}.${minorVersion}.${patchVersion}"
if (snapshot) {
    version += "-SNAPSHOT-${buildNumber}"
}

dependencies {
    implementation project(':nms:common')
    implementation project(':nms:versions:v1_20_R4')

    implementation project(':common')
    implementation project(':modules:mines')
    implementation project(':modules:omnitool')
    implementation project(':modules:explosives')
    implementation project(':modules:armor')
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

// Not needed on Paper 1.20.5+
//assemble.dependsOn(reobfJar)
assemble.dependsOn(shadowJar)

shadowJar {
    setArchiveClassifier(null)
    setArchiveFileName(getArchiveFileName().get().replace("-dev-all", ""))
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'

    filesMatching('paper-plugin.yml') {
        expand props
    }

    // Ensure the config.yml is included in the JAR
    from(sourceSets.main.resources.srcDirs) {
        include 'explosives/rpgconfig.yml'
    }

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}